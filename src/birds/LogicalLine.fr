
--- Input handling

module birds.LogicalLine where

-- import Data.List

--- we use JLine package for terminal input
data ConsoleReader = native jline.ConsoleReader where
    native new :: () -> IO (MutableIO ConsoleReader) throws IOException
    native setPrompt setDefaultPrompt :: MutableIO ConsoleReader -> String -> IO ()
    native readLine  :: MutableIO ConsoleReader -> IO (Maybe String) throws IOException
                      | MutableIO ConsoleReader -> String -> IO (Maybe String) throws IOException
    native addCompletor :: MutableIO ConsoleReader -> MutableIO Completor -> IO Bool

data Completor = native jline.Completor

class LogicalLineReader rdr where
    getNext :: (MutableIO rdr) -> IO (Maybe String)


--- Tell the number of missing right parentheses
missing :: String -> Int
missing = miss 0 . unpacked
    where
        miss !n []          = n
        miss !n ('(':cs)    = miss (n+1) cs
        miss !n (')':cs)    = miss (n-1) cs
        miss !n (_:cs)      = miss n cs


instance LogicalLineReader ConsoleReader where
    getNext :: MutableIO ConsoleReader -> IO (Maybe String)
    getNext cr = next "-> " ""
        where
            next prompt sofar = do 
                input <- cr.readLine prompt :: IO (Maybe String)
                case input of
                    Nothing
                        | null sofar  -> return Nothing
                        | otherwise   -> return (Just sofar)
                    Just line -> do
                        let r = sofar ++ (if length sofar > 0 then " " else "") ++ line
                            m = missing r
                        if m > 0
                        then next ("(" ++ show m ++ ") ") r
                        else return (Just r) 
                        
                  
instance LogicalLineReader BufferedReader where
    getNext cr = next ""
        where
            next sofar = do 
                input <- cr.readLine :: IO (Maybe String)
                case input of
                    Nothing
                        | null sofar  -> return Nothing
                        | otherwise   -> return (Just sofar)
                    Just line -> do
                        let r = sofar ++ (if length sofar > 0 then " " else "") ++ line
                            m = missing r
                        if m > 0
                        then next r
                        else return (Just r) 
 
    